precision highp float;

// Core beam/flare shaping and dynamics
#define PI 3.14159265359
#define TWO_PI 6.28318530718
#define EPS 1e-6
#define DT_LOCAL 0.0038
#define EDGE_SOFT (DT_LOCAL * 4.0)
#define TAP_RADIUS 6
#define R_H 150.0
#define R_V 150.0
#define FLARE_HEIGHT 16.0
#define FLARE_AMOUNT 8.0
#define FLARE_EXP 2.0
#define TOP_FADE_START 0.1
#define TOP_FADE_EXP 1.0
#define FLOW_PERIOD 0.5
#define FLOW_SHARPNESS 1.5

// Wisps (animated micro-streaks) that travel along the beam
#define W_BASE_X 1.5
#define W_LAYER_GAP 0.25
#define W_LANES 10
#define W_SIDE_DECAY 0.5
#define W_HALF 0.01
#define W_AA 0.15
#define W_CELL 20.0
#define W_SEG_MIN 0.01
#define W_SEG_MAX 0.55
#define W_CURVE_AMOUNT 15.0
#define W_CURVE_RANGE (FLARE_HEIGHT - 3.0)
#define W_BOTTOM_EXP 10.0

// Uniforms provided by LaserFlow component
uniform float iTime;
uniform vec3 iResolution;
uniform vec4 iMouse;
uniform float uWispDensity;
uniform float uTiltScale;
uniform float uFlowTime;
uniform float uFogTime;
uniform float uBeamXFrac;
uniform float uBeamYFrac;
uniform float uFlowSpeed;
uniform float uVLenFactor;
uniform float uHLenFactor;
uniform float uFogIntensity;
uniform float uFogScale;
uniform float uWSpeed;
uniform float uWIntensity;
uniform float uFlowStrength;
uniform float uDecay;
uniform float uFalloffStart;
uniform float uFogFallSpeed;
uniform vec3 uColor;
uniform float uFade;

// Volumetric fog controls
#define FOG_ON 1
#define FOG_CONTRAST 1.2
#define FOG_SPEED_U 0.1
#define FOG_SPEED_V -0.1
#define FOG_OCTAVES 5
#define FOG_BOTTOM_BIAS 0.8
#define FOG_TILT_TO_MOUSE 0.05
#define FOG_TILT_DEADZONE 0.01
#define FOG_TILT_MAX_X 0.35
#define FOG_TILT_SHAPE 1.5
#define FOG_BEAM_MIN 0.0
#define FOG_BEAM_MAX 0.75
#define FOG_MASK_GAMMA 0.5
#define FOG_EXPAND_SHAPE 12.2
#define FOG_EDGE_MIX 0.5

// Horizontal vignette for the fog volume
#define HFOG_EDGE_START 0.20
#define HFOG_EDGE_END 0.98
#define HFOG_EDGE_GAMMA 1.4
#define HFOG_Y_RADIUS 25.0
#define HFOG_Y_SOFT 60.0

// Beam extents and edge masking
#define EDGE_X0 0.22
#define EDGE_X1 0.995
#define EDGE_X_GAMMA 1.25
#define EDGE_LUMA_T0 0.0
#define EDGE_LUMA_T1 2.0
#define DITHER_STRENGTH 1.0

float g(float x){return x<=.00031308?12.92*x:1.055*pow(x,1./2.4)-.055;}
float bs(vec2 p,vec2 q,float powr){
    float d=distance(p,q),f=powr*uFalloffStart,r=(f*f)/(d*d+EPS);
    return powr*min(1.,r);
}
float bsa(vec2 p,vec2 q,float powr,vec2 s){
    vec2 d=p-q;float dd=(d.x*d.x)/(s.x*s.x)+(d.y*d.y)/(s.y*s.y),f=powr*uFalloffStart,r=(f*f)/(dd+EPS);
    return powr*min(1.,r);
}
float tri01(float x){float f=fract(x);return 1.-abs(f*2.-1.);}
float tauWf(float t,float tmin,float tmax){float a=smoothstep(tmin,tmin+EDGE_SOFT,t),b=1.-smoothstep(tmax-EDGE_SOFT,tmax,t);return max(0.,a*b);}
float h21(vec2 p){p=fract(p*vec2(123.34,456.21));p+=dot(p,p+34.123);return fract(p.x*p.y);}
float vnoise(vec2 p){
    vec2 i=floor(p),f=fract(p);
    float a=h21(i),b=h21(i+vec2(1,0)),c=h21(i+vec2(0,1)),d=h21(i+vec2(1,1));
    vec2 u=f*f*(3.-2.*f);
    return mix(mix(a,b,u.x),mix(c,d,u.x),u.y);
}
float fbm2(vec2 p){
    float v=0.,amp=.6;mat2 m=mat2(.86,.5,-.5,.86);
    for(int i=0;i<FOG_OCTAVES;++i){v+=amp*vnoise(p);p=m*p*2.03+17.1;amp*=.52;}
    return v;
}
float rGate(float x,float l){float a=smoothstep(0.,W_AA,x),b=1.-smoothstep(l,l+W_AA,x);return max(0.,a*b);}
float flareY(float y){float t=clamp(1.-(clamp(y,0.,FLARE_HEIGHT)/max(FLARE_HEIGHT,EPS)),0.,1.);return pow(t,FLARE_EXP);}

float vWisps(vec2 uv,float topF){
    float y=uv.y,yf=(y+uFlowTime*uWSpeed)/W_CELL;
    float dRaw=clamp(uWispDensity,0.,2.),d=dRaw<=0.?1.:dRaw;
    float lanesF=floor(float(W_LANES)*min(d,1.)+.5);// WebGL1-safe
    int lanes=int(max(1.,lanesF));
    float sp=min(d,1.),ep=max(d-1.,0.);
    float fm=flareY(max(y,0.)),rm=clamp(1.-(y/max(W_CURVE_RANGE,EPS)),0.,1.),cm=fm*rm;
    const float G=.05;float xS=1.+(FLARE_AMOUNT*W_CURVE_AMOUNT*G)*cm;
    float sPix=clamp(y/R_V,0.,1.),bGain=pow(1.-sPix,W_BOTTOM_EXP),sum=0.;
    for(int s=0;s<2;++s){
        float sgn=s==0?-1.:1.;
        for(int i=0;i<W_LANES;++i){
            if(i>=lanes)break;
            float off=W_BASE_X+float(i)*W_LAYER_GAP,xc=sgn*(off*xS);
            float dx=abs(uv.x-xc),lat=1.-smoothstep(W_HALF,W_HALF+W_AA,dx),amp=exp(-off*W_SIDE_DECAY);
            float seed=h21(vec2(off,sgn*17.)),yf2=yf+seed*7.,ci=floor(yf2),fy=fract(yf2);
            float seg=mix(W_SEG_MIN,W_SEG_MAX,h21(vec2(ci,off*2.3)));
            float spR=h21(vec2(ci,off+sgn*31.)),seg1=rGate(fy,seg)*step(spR,sp);
            if(ep>0.){float spR2=h21(vec2(ci*3.1+7.,off*5.3+sgn*13.));float f2=fract(fy+.5);seg1+=rGate(f2,seg*.9)*step(spR2,ep);}
            sum+=amp*lat*seg1;
        }
    }
    float span=smoothstep(-3.,0.,y)*(1.-smoothstep(R_V-6.,R_V,y));
    return uWIntensity*sum*topF*bGain*span;
}

void mainImage(out vec4 fc,in vec2 frag){
    vec2 C=iResolution.xy*.5;float invW=1./max(C.x,1.);
    float sc=512./iResolution.x*.4;
    vec2 uv=(frag-C)*sc,off=vec2(uBeamXFrac*iResolution.x*sc,uBeamYFrac*iResolution.y*sc);
    vec2 uvc=uv-off;
    float a=0.,b=0.;
    float basePhase=1.5*PI+uDecay*.5;float tauMin=basePhase-uDecay;float tauMax=basePhase;
    float cx=clamp(uvc.x/(R_H*uHLenFactor),-1.,1.),tH=clamp(TWO_PI-acos(cx),tauMin,tauMax);
    for(int k=-TAP_RADIUS;k<=TAP_RADIUS;++k){
        float tu=tH+float(k)*DT_LOCAL,wt=tauWf(tu,tauMin,tauMax);if(wt<=0.)continue;
        float spd=max(abs(sin(tu)),.02),u=clamp((basePhase-tu)/max(uDecay,EPS),0.,1.),env=pow(1.-abs(u*2.-1.),.8);
        vec2 p=vec2((R_H*uHLenFactor)*cos(tu),0.);
        a+=wt*bs(uvc,p,env*spd);
    }
    float yPix=uvc.y,cy=clamp(-yPix/(R_V*uVLenFactor),-1.,1.),tV=clamp(TWO_PI-acos(cy),tauMin,tauMax);
    for(int k=-TAP_RADIUS;k<=TAP_RADIUS;++k){
        float tu=tV+float(k)*DT_LOCAL,wt=tauWf(tu,tauMin,tauMax);if(wt<=0.)continue;
        float yb=(-R_V)*cos(tu),s=clamp(yb/R_V,0.,1.),spd=max(abs(sin(tu)),.02);
        float env=pow(1.-s,.6)*spd;
        float cap=1.-smoothstep(TOP_FADE_START,1.,s);cap=pow(cap,TOP_FADE_EXP);env*=cap;
        float ph=s/max(FLOW_PERIOD,EPS)+uFlowTime*uFlowSpeed;
        float fl=pow(tri01(ph),FLOW_SHARPNESS);
        env*=mix(1.-uFlowStrength,1.,fl);
        float yp=(-R_V*uVLenFactor)*cos(tu),m=pow(smoothstep(FLARE_HEIGHT,0.,yp),FLARE_EXP),wx=1.+FLARE_AMOUNT*m;
        vec2 sig=vec2(wx,1.),p=vec2(0.,yp);
        float mask=step(0.,yp);
        b+=wt*bsa(uvc,p,mask*env,sig);
    }
    float sPix=clamp(yPix/R_V,0.,1.),topA=pow(1.-smoothstep(TOP_FADE_START,1.,sPix),TOP_FADE_EXP);
    float L=a+b*topA;
    float w=vWisps(vec2(uvc.x,yPix),topA);
    float fog=0.;
    #if FOG_ON
    vec2 fuv=uvc*uFogScale;
    float mAct=step(1.,length(iMouse.xy)),nx=((iMouse.x-C.x)*invW)*mAct;
    float ax=abs(nx);
    float stMag=mix(ax,pow(ax,FOG_TILT_SHAPE),.35);
    float st=sign(nx)*stMag*uTiltScale;
    st=clamp(st,-FOG_TILT_MAX_X,FOG_TILT_MAX_X);
    vec2 dir=normalize(vec2(st,1.));
    fuv+=uFogTime*uFogFallSpeed*dir;
    vec2 prp=vec2(-dir.y,dir.x);
    fuv+=prp*(.08*sin(dot(uvc,prp)*.08+uFogTime*.9));
    float n=fbm2(fuv+vec2(fbm2(fuv+vec2(7.3,2.1)),fbm2(fuv+vec2(-3.7,5.9)))*.6);
    n=pow(clamp(n,0.,1.),FOG_CONTRAST);
    float pixW=1./max(iResolution.y,1.);
    #ifdef GL_OES_standard_derivatives
    float wL=max(fwidth(L),pixW);
    #else
    float wL=pixW;
    #endif
    float m0=pow(smoothstep(FOG_BEAM_MIN-wL,FOG_BEAM_MAX+wL,L),FOG_MASK_GAMMA);
    float bm=1.-pow(1.-m0,FOG_EXPAND_SHAPE);bm=mix(bm*m0,bm,FOG_EDGE_MIX);
    float yP=1.-smoothstep(HFOG_Y_RADIUS,HFOG_Y_RADIUS+HFOG_Y_SOFT,abs(yPix));
    float nxF=abs((frag.x-C.x)*invW),hE=1.-smoothstep(HFOG_EDGE_START,HFOG_EDGE_END,nxF);hE=pow(clamp(hE,0.,1.),HFOG_EDGE_GAMMA);
    float hW=mix(1.,hE,clamp(yP,0.,1.));
    float bBias=mix(1.,1.-sPix,FOG_BOTTOM_BIAS);
    float browserFogIntensity=uFogIntensity;
    browserFogIntensity*=1.8;
    float radialFade=1.-smoothstep(0.,.7,length(uvc)/120.);
    float safariFog=n*browserFogIntensity*bBias*bm*hW*radialFade;
    fog=safariFog;
    #endif
    float LF=L+fog;
    float dith=(h21(frag)-.5)*(DITHER_STRENGTH/255.);
    float tone=g(LF+w);
    vec3 col=tone*uColor+dith;
    float alpha=clamp(g(L+w*.6)+dith*.6,0.,1.);
    float nxE=abs((frag.x-C.x)*invW),xF=pow(clamp(1.-smoothstep(EDGE_X0,EDGE_X1,nxE),0.,1.),EDGE_X_GAMMA);
    float scene=LF+max(0.,w)*.5,hi=smoothstep(EDGE_LUMA_T0,EDGE_LUMA_T1,scene);
    float eM=mix(xF,1.,hi);
    col*=eM;alpha*=eM;
    col*=uFade;alpha*=uFade;
    fc=vec4(col,alpha);
}

void main(){
    vec4 fc;
    mainImage(fc,gl_FragCoord.xy);
    gl_FragColor=fc;
}